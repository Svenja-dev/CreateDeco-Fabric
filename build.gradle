import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '1.13-SNAPSHOT'
    id 'maven-publish'
}

repositories {
    maven {
        name "REI"
        url "https://maven.shedaniel.me/"
        content {
            includeGroup "me.shedaniel"
            includeGroup "me.shedaniel.cloth"
            includeGroup "dev.architectury"
        }
    }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        url "https://maven.tterrag.com/"
        content {
            includeGroup "com.tterrag.registrate"
        }
    }
    maven {
        name = 'CurseMaven'
        url = 'https://cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
}

version = "$project.mod_version"
group = project.maven_group

base {
    archivesName = "$project.archives_base_name-$project.minecraft_version"
}

loom {
    splitEnvironmentSourceSets()

    mods {
        createdeco {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

tasks.register("serverJar", Jar) {
    archiveClassifier = "server-dev"
    destinationDirectory = layout.buildDirectory.dir("tmp/serverJar")
    from sourceSets.main.output
}

tasks.register('remapServerJar', RemapJarTask) {
    dependsOn tasks.serverJar
    inputFile = tasks.serverJar.archiveFile
    archiveClassifier = "server"
}

tasks.build {
    dependsOn tasks.remapServerJar
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    if (project.compile_only_fabric_api.equals("true")) {
        modCompileOnly fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version)
        modCompileOnly fabricApi.module("fabric-transfer-api-v1", project.fabric_version)
        modCompileOnly fabricApi.module("fabric-events-interaction-v0", project.fabric_version)
        modCompileOnly fabricApi.module("fabric-renderer-api-v1", project.fabric_version)
        modCompileOnly fabricApi.module("fabric-recipe-api-v1", project.fabric_version)
    } else {
        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    }

    // Create Fly (Fabric port of Create) - Direct implementation without Loom remapping
    // This JAR is already in Yarn mappings, no remapping needed
    implementation files("libs/create-fly-1.21.10-6.0.8-3.jar")

    // Recipe Viewers (optional)
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
    if (project.compile_only_rei.equals("false")) {
        modClientRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
    }
    modCompileOnly "maven.modrinth:jei:${project.jei_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }

    exclude "_data"
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()
    tasks.named("sourcesJar", Jar) {
        exclude '_data'
        exclude 'assets'
        exclude 'data'
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}"}
    }
}
